plugins {
    id 'groovy'
    id 'application'
    id 'org.openjfx.javafxplugin'           version '0.0.8'
    id 'org.beryx.jlink'                    version '2.14.0'
    id 'org.javamodularity.moduleplugin'    version "1.5.0"
}

dependencies {
    implementation project (':supernautfx')

    implementation "javax.inject:javax.inject:1"
    implementation "io.micronaut:micronaut-inject-java:1.1.4"

    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation "org.slf4j:slf4j-jdk14:${slf4jVersion}"

    annotationProcessor "io.micronaut:micronaut-inject-java:1.1.4"
}

sourceCompatibility = 11
targetCompatibility = 11

mainClassName = "$moduleName/org.consensusj.supernautfx.sample.hello.HelloSupernautFxApp"

patchModules.config = [
        "java.annotation=jsr305-3.0.2.jar"
]

javafx {
    modules = ['javafx.controls', 'javafx.fxml']
}

test {
    moduleOptions {
        runOnClasspath = true
    }
}

jlink {
    addExtraDependencies("javafx")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'Supernaut Hello'
        jvmArgs = []
    }
    mergedModule {
        requires 'java.logging'
        requires 'javafx.graphics'
        requires 'javafx.controls'
        requires 'javafx.fxml'

        forceMerge 'org.slf4j'
    }
    jpackage {
        skipInstaller = true
        imageOptions = ["--verbose", "--app-version", version]
        installerOptions = ["--app-version", version]
    }
}
