plugins {
    id 'groovy'
    id 'application'
    id 'org.openjfx.javafxplugin'           version '0.0.8'
    id 'org.beryx.jlink'                    version '2.14.1'
    id 'org.javamodularity.moduleplugin'    version "1.5.0"
}

sourceCompatibility = 11
targetCompatibility = 11

version = '0.0.1-SNAPSHOT'

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

repositories {
    maven { url 'https://dl.bintray.com/consensusj/maven' }
    maven { url 'https://dl.bintray.com/omni/maven' }           // OmniJ (RPC client)
    maven { url 'https://oss.sonatype.org/content/groups/public' }
}

def omnijVersion = "0.5.8"

dependencies {
    implementation "${bitcoinjGroup}:${bitcoinjArtifact}:${bitcoinjVersion}"
    implementation ("${bitcoinjGroup}:${bitcoinjWalletFxArtifact}:${bitcoinjVersion}") {
        exclude group: 'org.openjfx'    
    }

    implementation ("foundation.omni:omnij-jsonrpc:${omnijVersion}") {
        exclude group: 'org.bitcoinj'
    }

    implementation "io.reactivex.rxjava2:rxjava:2.2.9"

    implementation "org.consensusj.supernaut:supernautfx:${supernautVersion}"
    implementation "io.micronaut:micronaut-inject-java:1.1.0"
    implementation "javax.inject:javax.inject:1"

    implementation 'com.google.guava:guava:27.1-android'
    implementation 'de.jensd:fontawesomefx:8.0.0'

    implementation 'com.google.zxing:core:3.4.0'
    implementation 'com.google.zxing:javase:3.4.0'

    implementation ("com.github.sarxos:webcam-capture:0.3.12") {
        exclude group: 'com.nativelibs4java', module: 'bridj'
    }

    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation "org.slf4j:slf4j-jdk14:${slf4jVersion}"

    annotationProcessor "io.micronaut:micronaut-inject-java:1.1.0"

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

patchModules.config = [
        "java.annotation=jsr305-3.0.2.jar"
]

javafx {
    modules = [ 'javafx.graphics', 'javafx.controls', 'javafx.fxml' ]
}

//mainClassName = "$moduleName/omnijwallet.OmmiJWalletApp"
mainClassName = "omnijwallet.OmniJWalletApp"

jlink {
    addExtraDependencies("javafx")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'OmniJWallet'
        jvmArgs = []
    }
    mergedModule {
        requires 'java.logging'
        requires 'java.desktop'

        requires 'org.slf4j'

        requires 'javafx.graphics'
        requires 'javafx.controls'
        requires 'javafx.fxml'
    }
    jpackage {
        // See https://badass-jlink-plugin.beryx.org/releases/latest/#_jpackage for
        // where the plugin's jpackage task finds the path to the jpackage tool by default
        // (Currently you need to manually install a OpenJDK 13 Early Access Build of jpackage)
        skipInstaller = false

        // macOS options
        imageOptions = ["--verbose", "--resource-dir", "${projectDir}/src/macos/resource-dir", "--app-version", version]
        installerOptions = ["--app-version", version]

    }
}
